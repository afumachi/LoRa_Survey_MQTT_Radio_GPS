// Camada Física PHY to Framework
//
// RECEBE PACOTE DOWNLINK
// Recebe Pacote_DL do broker MQTT (publicado pelo Python N2_N3)
void Phy_mqtt_receive_DL() {

  // Parâmetros do LoRa caso primeira energização do módulo NodeMCU/ESP32
  if (primeiro_setup == 1) {
    LoRa.sleep();
    LoRa.setTxPower(txPower);
    LoRa.setSpreadingFactor(spreadingFactor);
    LoRa.setSignalBandwidth(signalBandwidth);
    LoRa.setCodingRate4(codingRateDenominator);
    LoRa.idle();  // Retorna ao modo standby/recepção

    Serial.println("SETUP INICIAL - Gateway LoRa Maximum Distance Configuration");
    // Zera flags e variáveis de controle
    aguardando_confirmacao_UL    = false;
    millis_inicio_aguarda_UL     = 0;
    tempo_radio                  = 0;
    recebe_comando_nova_radio    = 0;
    confirma_novo_radio_sensor   = 0;
    confirma_novo_radio_base = 0;
    confirma_novo_radio = 0;
    contadorDL = 0;
    contadorSS = 0;
    primeiro_setup = 0;
  }

  if (mqtt_dl_disponivel) {          // Flag setada no callback MQTT
    mqtt_dl_disponivel = false;

    // Copia payload recebido para PacoteDL
    for (int i = 0; i < TAMANHO_PACOTE; i++) {
      PacoteDL[i] = mqtt_dl_payload[i];
    }

    // ADICIONADO Variáveis de recebimento do valores de rádio LoRa
    valor_novo_spreadingfactor = PacoteDL[0];  // Byte DL[0] valor de rádio LoRa de Spreading Spectrum
    valor_novo_bandwidth = PacoteDL[1];        // Byte DL[1] valor de rádio LoRa de Bandwidth

    // Configura Valor de Bandwidth de acordo com o valor recebido no Byte[1]
    if (valor_novo_bandwidth == 3) {
      valor_novo_bandwidth = 500E3;
    } else if (valor_novo_bandwidth == 2) {
      valor_novo_bandwidth = 250E3;
    } else if (valor_novo_bandwidth == 1) {
      valor_novo_bandwidth = 125E3;
    } 
    
    valor_novo_codingrate = PacoteDL[2];         // Byte DL[2] valor de rádio LoRa de CodingRate
    valor_novo_potencia_radio = PacoteDL[3];     // Byte DL[3] valor de rádio LoRa de Potência de Rádio LoRa
    tempo_radio = PacoteDL[6];                   // Byte DL[6] Recebe tempo de radio tx rx
    recebe_comando_nova_radio = PacoteDL[7];     // Byte DL[7] Recebe comando de reconfiguração de Rádio LoRa

    contador_medidas_total = (((PacoteDL[4])*256) + (PacoteDL[5])); // Número Total de medidas de teste LSS
    contador_medidas = (((PacoteDL[12])*256) + (PacoteDL[13]));     // Número de medidas corrente do teste LSS

    // Controla Estado do Teste LSS - analisa Byte Pacote_DL[7]
    if (recebe_comando_nova_radio != 10){

      if ((recebe_comando_nova_radio == 1) ){
          // Nova configuração de Rádio
          confirma_novo_radio_base = 2;
      }
      else if (recebe_comando_nova_radio == 3){
        // Teste de Enlace
        confirma_novo_radio_base = 3;
      }
      else if (recebe_comando_nova_radio == 4){
        // LSS em Andamento
        confirma_novo_radio_base = 4;
      }
      else if (recebe_comando_nova_radio == 5){
        // Último pacote do LoRa Site Survey
        confirma_novo_radio_base = 5;
      }

      Phy_radio_send_DL();  // chama a funcao de recepcao da camada de controle de acesso ao meio

    }
    else{
      // se igual a 10
      // retorna configuração parâmetros rádio Gateway para LMDC
      Serial.println("[Reconfiguração parâmetros rádio Gateway para LMDC]");
      confirma_novo_radio_base = 10;
      Phy_mqtt_send_UL();    
    
    }
  }
}

// ENVIA PACOTE DL PARA NÓ SENSOR ATRAVÉS DO RF95
// O pacote DL recebido pelo MQTT proveniente do Nível 2_3 é enviado via LoRa para o Nó Sensor
void Phy_radio_send_DL() {

  // Pisca o LED de transmissão de pacote DL
  digitalWrite(PIN_LED_VERMELHO, HIGH);  // Início da Transmissão

  LoRa.beginPacket();  // start packet
  for (int i = 0; i < TAMANHO_PACOTE; i++) {
    LoRa.write(PacoteDL[i]);  // add data to packet
  }
  LoRa.endPacket();  // finish packet and send it

  // Pisca o LED de transmissão de pacote DL
  digitalWrite(PIN_LED_VERMELHO, LOW);  // FIM da Transmissão

}

//==================================================================================================================
//======================= PACOTE UL LINK - PACODE VINDO NÓ SENSOR ENCAMINHADO PARA PYTHON===========================
//==================================================================================================================
// Pacote que chega no RF95 vindo do nó sensor e é passado para o buffer de TX da serial
//--------------------------- RECEBE PACOTE UL VINDO DO NÓ SENSOR ATRAVÉS DO MÓDULO RF95

void Phy_radio_receive_UL() {
  
  // Escuta o Rádio LoRa se identificou algum Pacote
  uint8_t packetSize = LoRa.parsePacket();

  // Caso positivo, identifica o tamanho do Payload do Pacote
  if (packetSize) {

    digitalWrite(PIN_LED_VERDE, HIGH);  // Apaga Led Verde Indicando Inicio da leitura do Pacote

    // Realiza a leitura caso Payload do Pacote seja compatível com o Pacote de 52 Bytes
    if (packetSize >= TAMANHO_PACOTE) {

      for (int i = 0; i < TAMANHO_PACOTE; i++) {
        PacoteUL[i] = LoRa.read();
      }
  
      // zera controle standby caso recebido Pacote_UL
      millis_standby_controle = millis();
      
      RSSI_dBm_UL = LoRa.packetRssi();
      SNR_UL_bruto = LoRa.packetSnr();

      //===================================== IMPORTANTE - OPÇÃO VERIFICAÇÃO DE ENDEREÇO OU MODO PROMÍSCUO========================
      // Quando recebe o pacote a base pode verificar o endereço de destino ou trabalhar em modo promíscuo.
      //===================== QUANDO A BASE  VERIFICA O ENDENREÇO DE DESTINO O PACOTE SÓ É ENVIADO PARA A SERIAL CASO A BASE SEJA O DESTINATÁRIO - nesse caso descomentar o bloco abaixo
      // Esta é uma função originalmente da camada de rede, mas existe um cross-layer para verificação do endereço de destino, recebendo somente os pacotes que são destinados para a base
  
      digitalWrite(PIN_LED_VERDE, LOW);  // Fim da leitura do Pacote
      //Serial.println("Pacote UPLINK Recebido");

      // Garante que Nó Sensor também recebeu comando de alteração de rádio e confirmou
      confirma_novo_radio_sensor = PacoteUL[7];  // PacoteUL[7] recebe confirmação do nó sensor do recebimento

      Phy_mqtt_send_UL();

    }
  }
}

// ==================== PUBLICA PACOTE UL NO BROKER MQTT ==============
// Substitui Phy_serial_send_UL(): envia o pacote UL ao Python via MQTT
void Phy_mqtt_send_UL() {

  // Calcula RSSI linear e aloca em 1 Byte conforme Documentação RFM95 de -10,5 dBm a -138dBm

  // Determina o offset baseado na frequência usada
  // (Ajuste para 164 se estiver usando 433MHz)
  int offset = 157; // Offset 157 para Frequência de 915MHz

  // Recupera o valor bruto (PacketRssi) para aplicar a nova fórmula
  int Rssi_UL_bruto = RSSI_dBm_UL + offset;

  if (SNR_UL_bruto >= 0) {
    // Sua fórmula com correção de linearidade (16/15)
    RSSI_dBm_UL = ((1.0666 * Rssi_UL_bruto) - offset); // 16/15 * RSSI - 157
  } else {
    // Fórmula para sinal abaixo do ruído (SNR < 0)
    RSSI_dBm_UL = ((Rssi_UL_bruto - offset) + (SNR_UL_bruto));
  }  

  //--- Bloco que faz adequação da leitura de RSSI para um byte ---
  if (RSSI_dBm_UL > -10.5)   // Caso a RSSI medida esteja acima do valor superior -10,5 dBm
  {
    RSSI_UL = 127;  // equivalente a -10,5 dBm
  }

  if (RSSI_dBm_UL <= -10.5 && RSSI_dBm_UL >= -74)  // Caso a RSSI medida esteja no intervalo [-10,5 dBm e -74 dBm]
  {
    RSSI_UL = ((RSSI_dBm_UL + 74) * 2);
  }

  if (RSSI_dBm_UL < -74)  // Caso a RSSI medida esteja no intervalo ]-74 dBm e -138 dBm]
  {
    RSSI_UL = (((RSSI_dBm_UL + 74) * 2) + 256);
  }

  // 1. Trava o valor entre -30 e +30 para evitar que o byte estoure
  if (SNR_UL_bruto < -30.0) SNR_UL_bruto = -30.0;
  if (SNR_UL_bruto > 30.0) SNR_UL_bruto = 30.0;

  // Usamos uint8_t (byte) para ocupar apenas 1 byte na memória.
  // Usamos a função round() para garantir que o número float seja 
  // arredondado corretamente antes de virar inteiro.
  SNR_UL = (uint8_t)round((SNR_UL_bruto + 30.0) * 4.0); // Offset de 30.0dB e passo de 0.25dB (* 4.0)

  // =================Informações de gerência do pacote
  PacoteUL[2] = RSSI_UL;  // aloca RSSI_UL
  PacoteUL[3] = SNR_UL;

  if ((confirma_novo_radio_base == 2) & (confirma_novo_radio_sensor == 2)){
    // Confirmação do segundo ciclo de ambos devices
    primeiro_setup = 0;
    PacoteUL[7] = 3;
    confirma_novo_radio = 1;
            
    valor_anterior_spreadingfactor = valor_atual_spreadingfactor;
    valor_anterior_bandwidth = valor_atual_bandwidth;
    valor_anterior_codingrate = valor_atual_codingrate;
    valor_anterior_potencia_radio = valor_atual_potencia_radio;

    valor_atual_spreadingfactor = valor_novo_spreadingfactor;
    valor_atual_bandwidth = valor_novo_bandwidth;
    valor_atual_codingrate = valor_novo_codingrate;
    valor_atual_potencia_radio = valor_novo_potencia_radio;
        
  }
  else if ((confirma_novo_radio_base == 3) & (confirma_novo_radio_sensor == 3)){
    // Confirmação do terceiro ciclo de ambos devices já com Nova Configuração de Rádio
    PacoteUL[7] = 4;
    confirma_novo_radio = 0;    
  }
  else if ((confirma_novo_radio_base == 4) & (confirma_novo_radio_sensor == 4)){
    // Confirmação do terceiro ciclo de ambos devices já com Nova Configuração de Rádio
    PacoteUL[7] = 6;
    confirma_novo_radio = 0;    
  }  
  else if ((confirma_novo_radio_base == 1) & (confirma_novo_radio_sensor == 0)){
    // Indica ao Nível 3 que apenas um dos Devices LoRa (Base) recebeu/processou o Comando de alteração
    PacoteUL[7] = 1;
    confirma_novo_radio = 0;    
  }
  else if ((confirma_novo_radio_base == 0) & (confirma_novo_radio_sensor == 1)){
    // Indica ao Nível 3 que apenas um dos Devices LoRa (Nó Sensor) recebeu/processou o Comando de alteração
    PacoteUL[7] = 1;
    confirma_novo_radio = 0;    
  }
  else if (confirma_novo_radio_base == 5){
    PacoteUL[7] = 5;
    confirma_novo_radio = 5;
  }
  else {
    // Sem necessidade de alteração de Rádio
    PacoteUL[7] = 0;
    confirma_novo_radio = 0;    
  }


  if (confirma_novo_radio == 1) {
    AplicarConfiguracoesRadio();
  }

  if (confirma_novo_radio_base == 10){
    //RetornaConfiguracoesRadioMAX();
    Serial.println("Perda Enlace Nó Sensor - Retorna Configuracoes Radio LBDC");
    // Para limpar:
    memset(PacoteUL, 0, sizeof(PacoteUL));
    PacoteUL[RECEIVER_ID] = 0;
    PacoteUL[7] = 10;
    confirma_novo_radio_sensor = 0;
    confirma_novo_radio_base = 0;
    confirma_novo_radio = 0;
    recebe_comando_nova_radio = 0;
    contadorDL = 0;
    contadorSS = 0;

    reset_gateway_para_setup_inicial();

  }

  // --- Publica os 20 bytes no tópico UL, com QoS1 (at least once) ---
  // Importante para o dado de luminosidade, que é a peça central do framework:
  // o broker confirma (PUBACK) e a biblioteca retransmite se necessário.
  if (mqttClient.connected()) {
    bool ok = mqttClient.publish(TOPIC_UL, (char*)PacoteUL, TAMANHO_PACOTE, false, MQTT_QOS);
    if (ok) {
      Serial.println("Pacote UL publicado via MQTT (QoS1).");
    } else {
      Serial.println("Falha ao publicar pacote UL via MQTT.");
    }
  } else {
    Serial.println("MQTT desconectado – pacote UL descartado.");
  }


  if (confirma_novo_radio == 5){
    contadorDL = 0;
    confirma_novo_radio = 0;
    Serial.println("[PHY] CONFIRMA ÚLTIMO PACOTE - RESET RÁDIO");
    reset_gateway_para_setup_inicial();
  } 
 

}

void AplicarConfiguracoesRadio() {

  if (confirma_novo_radio == 1) {

    LoRa.sleep();                                         // Coloca em sleep para garantir a mudança de parâmetros
    LoRa.setTxPower(valor_novo_potencia_radio);           // Potência de Transmissão (Configurado em bibliotecas.h)
    LoRa.setSpreadingFactor(valor_novo_spreadingfactor);  // Fator de Espalhamento  (Configurado em bibliotecas.h)
    LoRa.setSignalBandwidth(valor_novo_bandwidth);        // Banda do Sinal (Configurado em bibliotecas.h)
    LoRa.setCodingRate4(valor_novo_codingrate);           // Coding Rate  (Configurado em bibliotecas.h)
    LoRa.idle();                                       // Retorna ao modo standby/recepção

    confirma_novo_radio_sensor = 0;
    confirma_novo_radio_base = 0;
    confirma_novo_radio = 0;
    recebe_comando_nova_radio = 0;
    primeiro_setup = 0;
  }
}


// =====================================================================
//                     Callback MQTT (recepção de mensagens)
// =====================================================================
// Callback "advanced" -- necessário para acessar o payload como bytes
// binários (o callback simples da lib entrega String, que corrompe
// bytes nulos/binários). Nunca chamar publish/subscribe aqui dentro
// (recomendação da própria biblioteca) -- só seta a flag.
void mqtt_callback(MQTTClient *client, char topic[], char bytes[], int length) {
  if (strcmp(topic, TOPIC_DL) == 0) {
    if (length >= TAMANHO_PACOTE) {
      for (int i = 0; i < TAMANHO_PACOTE; i++) {
        mqtt_dl_payload[i] = (byte)bytes[i];
      }
      mqtt_dl_disponivel = true;   // sinaliza para o loop principal
    }
  }
}

// =====================================================================
//                    Funções de conexão MQTT
// =====================================================================

void conectar_mqtt() {
  while (!mqttClient.connected()) {
    Serial.print("[MQTT] Conectando ao broker...");
    if (mqttClient.connect(CLIENT_ID.c_str())) {
      Serial.println(" conectado!");
      mqttClient.subscribe(TOPIC_DL, MQTT_QOS);
      Serial.print("[MQTT] Inscrito (QoS");
      Serial.print(MQTT_QOS);
      Serial.print(") em: ");
      Serial.println(TOPIC_DL);
    } else {
      Serial.print(" falhou (erro=");
      Serial.print((int)mqttClient.lastError());
      Serial.println("). Tentando em 300ms...");
      delay(300);
    }
  }
}

// ============================================================
// FUNÇÃO AUXILIAR - Reseta estado para aguardar novo downlink serial
// ============================================================

void reset_gateway_para_setup_inicial() {

  // Zera flags e variáveis de controle
  aguardando_confirmacao_UL    = false;
  millis_inicio_aguarda_UL     = 0;
  tempo_radio                  = 0;
  recebe_comando_nova_radio    = 0;
  confirma_novo_radio_sensor   = 0;
  confirma_novo_radio_base = 0;
  confirma_novo_radio = 0;
  contadorDL = 0;
  contadorSS = 0;

  // Retorna rádio aos parâmetros de SETUP
  primeiro_setup = 1;
  PacoteUL[7] = 10; // Gateway Resetou para Máxima
}

